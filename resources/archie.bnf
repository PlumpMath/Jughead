<ArchieLine> = Normal | Special
Special      = KeyValuePair | End      |
               Skip         | EndSkip  |
               OpenScope    | EndScope |
               OpenArray    | EndArray |
               PlainText    |
               Ignore       

(* Key Value Pair *)
KeyValuePair = <WhiteSpace>* Key <WhiteSpace>* <':'> <WhiteSpace>* Value
Value        = (ValueWord (WhiteSpace+ ValueWord)*) WhiteSpace*
<ValueWord>  = #'[^\r\n\s\t]+'
Key          = KeyPart (<'.'> KeyPart)*
<KeyPart>    = #'[\w\-]+'
End          = WhiteSpace* <#'(?i):end'> WhiteSpace* <AnyValue>

(* Skip / Endskip *)
Skip    = WhiteSpace* <#'(?i):skip'> WhiteSpace* <AnyValue>
EndSkip = WhiteSpace* <#'(?i):endskip'> WhiteSpace* <AnyValue>

(* Scope *)
OpenScope = <WhiteSpace>* <'{'> <WhiteSpace>* Key <WhiteSpace>* <'}'> <AnyValue>
EndScope  = <WhiteSpace>* <'{'> <WhiteSpace>* <'}'> <AnyValue>

(* Arrays *)
OpenArray = <WhiteSpace>* <'['> <WhiteSpace>* Key <WhiteSpace>* <']'> <AnyValue>
EndArray  = <WhiteSpace>* <'['> <WhiteSpace>* <']'> <AnyValue>
PlainText = <WhiteSpace>* <'*'> AnyValue

(* Ignore *)
Ignore = WhiteSpace* <#'(?i):ignore'> WhiteSpace* <AnyValue>

(* Normal *)
Normal = AnyValue WhiteSpace*

(* Utility *)
<AnyValue>   = #".*"
<WhiteSpace> = #"[\t\s]"
